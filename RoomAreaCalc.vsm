PROCEDURE RoomAreaCalc;
VAR
    gCount : INTEGER;

    FUNCTION GetRoomNameL(h : HANDLE) : HANDLE;
    VAR
        srcLayerName, destLayerName : STRING;
    BEGIN
        srcLayerName := GetLName(GetLayer(h));
        destLayerName := '';
        
        IF Pos('1', srcLayerName) > 0 THEN destLayerName := 'プラン建具・文字（1階）'
        ELSE IF Pos('2', srcLayerName) > 0 THEN destLayerName := 'プラン建具・文字（2階）'
        ELSE IF Pos('3', srcLayerName) > 0 THEN destLayerName := 'プラン建具点文字（3階）';
        GetRoomNameL := GetLayerByName(destLayername);
    END;


    PROCEDURE ProcessObject(h : HANDLE);
    VAR
        areaVal, tsuboVal : REAL;
        p1, p2 : POINT;
        p3 : POINT3D;
        kirisute : LONGINT;
        areaStr, tsuboStr, roomName, fullStr : STRING;
        hText, h3d : HANDLE;
        _, foundText : BOOLEAN;

        { 内包するテキストを探すためのサブ関数 }
        PROCEDURE CheckText(ht : HANDLE);
        VAR tx, ty : REAL;
        BEGIN
            IF (GetType(ht) = 10) AND (NOT foundText) THEN BEGIN
                HCenter(ht, tx, ty);
                { 図形の中心とテキストの中心が近い(例: 300mm以内)か判定 }
                IF PtInRect(tx, ty, p1.x, p1.y, p2.x, p2.y) THEN BEGIN
                    hText := ht;
                    foundText := True;
                END;
            END;
        END;

    BEGIN
        IF (GetType(h) = 3) OR (GetType(h) = 5) OR (GetType(h) = 21) THEN
        BEGIN
            gCount := gCount + 1;
            { 面積計算 }
            kirisute := Trunc(HArea(h) / 10000);
            areaVal := kirisute / 100;
            tsuboVal := Trunc((areaVal / 1.62) * 100) / 100; { 帖数も第2位切り捨て }
            
            areaStr := Concat(Num2Str(2, areaVal), '㎡');
            tsuboStr := Concat(Num2Str(2, tsuboVal), '帖');
            
            foundText := False;
            hText := NIL;

            h3d := HDuplicate(h, 0, 0);
            h3d := HExtrude(h3d, 0, 1);
            _ := Centroid3D(h3d, p3.x, p3.y, p3.z); 
            DelObject(h3d);
            GetBbox(h, p1.x, p1.y, p2.x, p2.y);

            { 1. 同じレイヤー内のテキストを走査して位置が近いものを探す }
            ForEachObject(CheckText, ALL); { すべての図形をチェック }

            { 既存テキストがあった場合、名前を抽出 }
            IF foundText THEN BEGIN
                fullStr := GetText(hText);
                IF Pos(Chr(13), fullStr) > 0 THEN
                    roomName := Copy(fullStr, 1, Pos(Chr(13), fullStr) - 1)
                ELSE
                    roomName := fullStr;
            END ELSE BEGIN
                roomName := 'BR';
            END;

            fullStr := Concat(roomName, Chr(13), areaStr, Chr(13), tsuboStr);

            IF foundText THEN BEGIN
                SetText(hText, fullStr);
                SetTextOrientation(hText, p3.x, p3.y, 0, FALSE);
            END ELSE BEGIN
                TextOrigin(p3.x, p3.y);
                BeginText; fullStr EndText;
                hText := LNewObj;
                { レイヤーを合わせる }
                IF SetParent(hText, GetParent(h)) THEN BEGIN END;
            END;

            SetTextJust(hText, 2);
            SetTextVerticalAlign(hText, 3);
            SetTextSize(hText, 0, 99, 5.8);
            SetTextFont(hText, 0, 99, GetFontId('ＭＳ Ｐゴシック'));
            _ := SetParent(hText, GetRoomNameL(h));
        END;
    END;

BEGIN
    gCount := 0;
    ForEachObject(ProcessObject, (SEL=TRUE));
    IF gCount = 0 THEN AlrtDialog('対象図形が見つかりません。');
    RedrawAll;
    DSelectObj(T=TEXT);
END;
Run(RoomAreaCalc);
